#Async Task

Create a new package `com.marakana.utils` containing an xtend class to encapsulate the twitter API:

##TwitterAPI.xtend

~~~java
package com.marakana.utils

import winterwell.jtwitter.Twitter

class TwitterAPI
{
  var Twitter twitter

  new ()
  {
    this.twitter = new Twitter("student", "password")
    twitter.setAPIRootUrl("http://yamba.marakana.com/api")
  }
   
  def String updateStatus (String status)
  { 
    val result = twitter.updateStatus(status)
    result.text
  }
}
~~~

Introduce an AsyncTask `com.marakana.utils` to use this API:

##TwitterPoster.xtend

~~~java
package com.marakana.yambax

import android.os.AsyncTask
import android.util.Log
import winterwell.jtwitter.TwitterException
import android.widget.Toast
import android.app.Activity
import com.marakana.utils.TwitterAPI

class TwitterPoster extends AsyncTask<String, Integer, String>
{
  var TwitterAPI twitter
  var Activity activity

  new(TwitterAPI twitter, Activity activity)
  {
    this.twitter = twitter
    this.activity = activity
  }

  override doInBackground(String... it)
  {
    try
    {
      var status = twitter.updateStatus(get(0))
      status
    }
    catch (TwitterException e)
    {
      Log.e("YAMBA", e.toString());
      "Failed to post";
    }
  }

  override onPostExecute(String result)
  {
    Toast.makeText(activity, result, Toast.LENGTH_LONG).show();
  }
}
~~~


Now we can reimplement StatusUpdate activity, using a Lambda to handle the button event:

~~~java
package com.marakana.yambax

import android.app.Activity
import android.os.Bundle
import android.widget.EditText
import android.widget.Button
import android.util.Log
import com.marakana.utils.TwitterAPI

class StatusActivity extends Activity
{
  val twitter = new TwitterAPI

  override onCreate(Bundle savedInstanceState)
  {
    super.onCreate(savedInstanceState)
    setContentView(R.layout.activity_status)
    
    val editText     = findViewById(R.id.editText) as EditText
    val updateButton =  findViewById(R.id.buttonUpdate) as Button
    
    updateButton.setOnClickListener = [ 
                                        val twitterPoster = new TwitterPoster(twitter, this)
                                        twitterPoster.execute(editText.getText().toString())
                                        Log.d("YAMBA", "onClicked") 
                                      ]
  }
}
~~~

Notice we no longer need to disable strict mode, as we are not posting from a background task.

Verify this works as expected, and that the status appears on:

- <http://yamba.marakana.com>

A toast message should also appear if the update was successful

---
####[Source](https://github.com/edeleastar/yambax/commit/b5d3d142bde4b5e1e73e5ce67f6212b1ff4d059d)
---

