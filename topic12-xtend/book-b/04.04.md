#BackgoundService Class

We define a new abstract class to encapsulate a general purpose background service in `com.marakana.utils`:

##BackgroundService.xtend

~~~java
package com.marakana.utils

import android.app.Service
import android.content.Intent

abstract class BackgroundService extends Service
{
  val DELAY   = 10000
  var running = false 
  var Thread updateThread
  
  var updater = [ | 
                  while (!Thread.currentThread().isInterrupted() && running)
                  {
                    try
                    {
                      this.doBackgroundTask()
                      Thread.sleep(DELAY);
                    }
                    catch (InterruptedException e)
                    {}
                  } ] as Runnable
  
  override onBind(Intent intent)
  {
    null
  } 

  def abstract void doBackgroundTask()
  
  def startBackgroundTask()
  {
    running = true
    updateThread.start
  }
  
  def stopBackgroundTask()
  {
    running = false
    updateThread.interrupt
  }
  
  override onCreate()
  {
    updateThread = new Thread(updater)
    super.onCreate
  }
}
~~~

This will allow us to simplify the UpdaterService class:

##UpdaterService.xtend

~~~java
package com.marakana.yambax

import android.content.Intent
import java.util.List
import winterwell.jtwitter.Twitter;
import com.marakana.utils.TwitterAPI
import winterwell.jtwitter.TwitterException
import android.util.Log
import com.marakana.utils.BackgroundService

class UpdaterService extends BackgroundService
{
  var TwitterAPI           twitter
  var List<Twitter.Status> timeline;
  
  override onBind(Intent intent)
  {
    null
  } 

  override def void doBackgroundTask()
  {
    try
    {
      timeline = twitter.getFriendsTimeline()
      timeline.forEach[ Log.d("YAMBA", String.format("%s: %s", it.user.name, it.text))]
    }
    catch (TwitterException e)
    {
      Log.e("YAMBA", "Failed to connect to twitter service", e); 
    }
  }
  
  override onCreate()
  {
    super.onCreate
    var app = getApplication() as YambaApplication
    this.twitter = app.twitter
  }

  override onStartCommand(Intent intent, int flags, int startId)
  {
    super.onStartCommand(intent, flags, startId)
    startBackgroundTask
    START_STICKY;
  }

  override onDestroy()
  {
    super.onDestroy
    stopBackgroundTask
  }
}
~~~

Test the application again.

---
####[Source](https://github.com/edeleastar/yambax/commit/b63666909763a3718aa559562d67acc58496bd7f)
---


